defaultTasks 'clean', 'install'

group = 'com.controlj.experiment.bulktrend'
version = '1.1'
apply plugin: 'java'
apply plugin: 'idea'

repositories {
   mavenCentral()
}

dependencies {
    compile 'org.apache.httpcomponents:httpclient:4.0.1'
    compile 'commons-cli:commons-cli:1.1'
    compile 'commons-lang:commons-lang:2.5'
    compile 'org.json:json:20080701'
    compile 'net.sf.opencsv:opencsv:2.0'
}

jar {
    from configurations.compile.collect {
      it.isDirectory() ? it : zipTree(it).matching { exclude 'META-INF/MANIFEST.MF' }
    }
    manifest {
      attributes('Main-Class': 'com.controlj.experiment.bulktrend.trendclient.Main')
    }
}



File launcherBuildDir = file('build/launchers')
String scriptName = 'trendclient.sh'
File script = new File(launcherBuildDir, scriptName)

task launchers(dependsOn: jar) << {
    launcherBuildDir.mkdirs()
    
    script.text = """#!/bin/bash
java -jar libs/${jar.archiveName} \$*
"""
    script.setExecutable(true, false);

    File batch = new File(launcherBuildDir, 'trendclient.bat')
    batch.text = """@echo off
java -jar lib\\${jar.archiveName} %*
"""    
}

task dist(type: Zip, dependsOn: launchers) {
    group = 'Distribution'
    baseName = 'trendclient'
    from(launcherBuildDir) {
        fileMode = 0755
    }
    from(jar.archivePath) {
        into 'lib'
    }
    from('src/main/doc')
}

task sourceDist(type:Zip, dependsOn:jar) {
    group = 'Distribution'
    baseName = 'trendClient'
    classifier = 'src'

    from('.') {
        exclude '*.iml'
        exclude '*.ipr'
        exclude '*.iws'
        exclude 'samples/'
        exclude 'build/'
        exclude '.gradle/'
    }
}

File installDir = file('build/distributions/exploded')
task install(type:Sync, dependsOn: dist) {
    into installDir
    from zipTree(dist.archivePath) 
} << {
    (new File(installDir, scriptName)).setExecutable(true, false)
    println "Installed into $installDir"
}


task wrapper(type: Wrapper) {
   gradleVersion = '0.9'
   urlRoot = 'http://gradle.artifactoryonline.com/gradle/distributions' 
   jarFile = file('wrapper/gradle-wrapper.jar')
   archiveBase = Wrapper.PathBase.GRADLE_USER_HOME
}
